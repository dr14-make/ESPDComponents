component ActiveSuspension
  wheel = DyadExampleComponents.MassSpringDamper(m = wheel_mass, d = wheel_damping, c = wheel_stiffness, g = -10, theta = pi / 2, s0 = wheel_initial_position) [{
    "Dyad": {
      "placement": {"diagram": {"x1": 830, "x2": 930, "y1": 650, "y2": 750, "rot": 90}}
    }
  }]
  car_and_suspension = DyadExampleComponents.MassSpringDamper(m = car_mass, d = suspension_damping, c = suspension_stiffness, g = -10, theta = pi / 2, s0 = suspension_initial_position) [{
    "Dyad": {
      "placement": {"diagram": {"x1": 830, "x2": 930, "y1": 500, "y2": 600, "rot": 90}}
    }
  }]
  seat = DyadExampleComponents.MassSpringDamper(m = human_and_seat_mass, d = seat_damping, c = seat_stiffness, g = -10, theta = pi / 2, s0 = seat_initial_position) [{
    "Dyad": {
      "placement": {"diagram": {"x1": 830, "x2": 930, "y1": 330, "y2": 430, "rot": 90}}
    }
  }]
  road_data = DyadExampleComponents.RoadData() [{
    "Dyad": {"placement": {"diagram": {"x1": 500, "y1": 730, "x2": 600, "y2": 830}}}
  }]
  road = DyadExampleComponents.SimplePosition() [{
    "Dyad": {"placement": {"diagram": {"x1": 650, "y1": 730, "x2": 750, "y2": 830}}}
  }]
  force = TranslationalComponents.Force() [{
    "Dyad": {
      "placement": {"diagram": {"x1": 640, "x2": 740, "y1": 330, "y2": 430, "rot": 270}}
    }
  }]
  pid = BlockComponents.LimPID(k = Kp, Ti = Ti, Td = Td) [{
    "Dyad": {"placement": {"diagram": {"x1": 480, "y1": 430, "x2": 580, "y2": 330}}}
  }]
  set_point = BlockComponents.Constant(k = 1.5) [{
    "Dyad": {"placement": {"diagram": {"x1": 310, "y1": 330, "x2": 410, "y2": 430}}}
  }]
  seat_pos = TranslationalComponents.PositionSensor() [{
    "Dyad": {
      "placement": {"diagram": {"x1": 690, "x2": 790, "y1": 160, "y2": 260, "rot": 180}}
    }
  }]
  parameter wheel_mass::Dyad.Mass = 25
  parameter wheel_stiffness::TranslationalSpringConstant = 1e2
  parameter wheel_damping::DampingCoefficient = 1e4
  parameter car_mass::Dyad.Mass = 1000
  parameter suspension_stiffness::TranslationalSpringConstant = 1e4
  parameter suspension_damping::DampingCoefficient = 10
  parameter human_and_seat_mass::Dyad.Mass = 100
  parameter seat_stiffness::TranslationalSpringConstant = 1000
  parameter seat_damping::DampingCoefficient = 1
  parameter wheel_initial_position::Dyad.Position = 0.5
  parameter suspension_initial_position::Dyad.Position = 1
  parameter seat_initial_position::Dyad.Position = 1.5
  parameter Kp::Real = 20
  parameter Ti::Real = 5
  parameter Td::Real = 1
relations
  u: analysis_point(pid.y, force.f)
  y: analysis_point(seat_pos.s, pid.u_m)
  pid.u_ff = 0
  connect(road.s, road_data.y) [{"Dyad": {"edges": [{"S": 2, "M": [], "E": 1}], "junctions": []}}]
  connect(wheel.flange_sd, road.flange) [{"Dyad": {"edges": [{"S": 1, "E": 2, "M": [{"x": 880, "y": 780}]}]}}]
  connect(car_and_suspension.flange_sd, wheel.flange_m) [{"Dyad": {"edges": [{"S": 1, "E": 2}]}}]
  connect(car_and_suspension.flange_m, seat.flange_sd, force.flange_a) [{
    "Dyad": {
      "edges": [
        {"S": -1, "E": 1},
        {"S": -1, "E": 2},
        {"S": -1, "E": 3, "M": [{"x": 690, "y": 450}]}
      ],
      "junctions": [{"x": 880, "y": 450}]
    }
  }]
  connect(seat.flange_m, force.flange_b, seat_pos.flange) [{
    "Dyad": {
      "edges": [
        {"S": -1, "E": 1},
        {"S": -1, "E": 2, "M": [{"x": 690, "y": 310}]},
        {"S": -1, "E": 3, "M": [{"x": 880, "y": 210}]}
      ],
      "junctions": [{"x": 880, "y": 310}]
    }
  }]
  #
  connect(pid.u_m, seat_pos.s) [{"Dyad": {"edges": [{"S": 1, "E": 2, "M": [{"x": 510, "y": 210}]}]}}]
  connect(set_point.y, pid.u_s) [{"Dyad": {"edges": [{"S": 1, "E": 2}]}}]
  connect(pid.y, force.f) [{"Dyad": {"edges": [{"S": 1, "E": 2}]}}]
metadata {
  "Dyad": {
    "tests": {
      "case1": {
        "stop": 10,
        "expect": {
          "signals": [
            "wheel.mass.s",
            "seat.mass.s",
            "car_and_suspension.mass.s",
            "wheel.mass.v",
            "car_and_suspension.mass.v",
            "seat.mass.v",
            "pid.y"
          ]
        }
      }
    }
  }
}
end